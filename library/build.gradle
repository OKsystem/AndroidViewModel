apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test:rules:1.1.2-alpha01'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
 
task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}
 
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
 
artifacts {
    archives androidSourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = GROUP
        pom.artifactId = 'androidviewmodel'
        pom.version = VERSION_NAME
        repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
        repository url: "http://" + MAVEN_REPO + "-${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
    }
}

task install(dependsOn: uploadArchives)
